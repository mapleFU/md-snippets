use std::str::FromStr;
use std::rc::Rc;
use std::collections::LinkedList;

use crate::calculator_ast;

grammar;

// pub Calcllist: i32 = {
//     <c: Calcllist> "\n" <e: Expr> => {
//         println!("= {:?}", e);
//         0
//     },
//     <c: Calcllist> "\n" => {
//         print!("> ");
//         0
//     },
// };

pub Statement: calculator_ast::Expr = {
    <e: Expr> => {
        e
    },
    IF <e: Expr> THEN <l1: List> ELSE <l2: List> => {
        let cond = Box::new(e);
        let if_branch = l1;
        let else_branch = l2;

        let flow = calculator_ast::ControlFlow::Condition(calculator_ast::IfCondition {
            cond,
            if_branch,
            else_branch: Some(else_branch),
        });

        calculator_ast::Expr::Flow(flow)
    },
    IF <e: Expr> THEN <l: List> => {
        let cond = Box::new(e);
        let if_branch = l;

        let flow = calculator_ast::ControlFlow::Condition(calculator_ast::IfCondition {
            cond,
            if_branch,
            else_branch: None,
        });

        calculator_ast::Expr::Flow(flow)
    },
}; 

pub List: calculator_ast::ExprList = {
    () => {
        calculator_ast::ExprList(None)
    },
    <stmt: Statement> ";" <l: List> => {
        if l.0.is_none() {
            let mut new_list = LinkedList::default();
            new_list.push_back(Rc::new(stmt));
            calculator_ast::ExprList(Some(new_list))
        } else {
            let mut new_list = l.0.unwrap().clone();
            new_list.push_back(Rc::new(stmt));
            calculator_ast::ExprList(Some(new_list))
        }
    }
};

pub Expr: calculator_ast::Expr = {
    MulExpr,
    // 这个表达式是右结合的，所以在右侧
    <s: VarName> "=" <e: Expr> => {
        calculator_ast::Expr::Assign(
            s.into(),
            Box::new(e),
        )
    },
}

// Expr is for mul / div
pub MulExpr: calculator_ast::Expr = {
    Factor,
    <e: MulExpr> "+" <f: Factor> => {
        let left = Box::new(e);
        let right = Box::new(f);
        calculator_ast::Expr::TwoOp(
            calculator_ast::Opcode::Add,
            left,
            right
        )
    },
    <e: MulExpr> "-" <f: Factor> => {
        let left = Box::new(e);
        let right = Box::new(f);
        calculator_ast::Expr::TwoOp(
            calculator_ast::Opcode::Sub,
            left,
            right
        )
    },
};


pub Factor: calculator_ast::Expr = {
    Num,
    <f: Factor> "*" <n: Num> => {
        let left = Box::new(f);
        let right = Box::new(n);
        calculator_ast::Expr::TwoOp(
            calculator_ast::Opcode::Mul,
            left,
            right
        )
    },
    <f: Factor> "/" <n: Num> => {
        let left = Box::new(f);
        let right = Box::new(n);
        calculator_ast::Expr::TwoOp(
            calculator_ast::Opcode::Div,
            left,
            right
        )
    },
};

// only pub will generate parser.
pub Num: calculator_ast::Expr = { 
    <s:r"[0-9]+"> => { 
        // return a number expr.
        calculator_ast::Expr::Number(
            calculator_ast::Number::from_i64(i64::from_str(s).unwrap())
        )
    },
    "(" <e: Expr> ")" => {
        // Just return itself
        e
    },
    "-" <n: Num> => {
        let node = Box::new(n);
        calculator_ast::Expr::OneOp(
            calculator_ast::Opcode::Sub,
            node
        )
    },
    <s: r"[0-9]+\.[0-9]*([Ee][-+]?[0-9]+)?"> => {
        let fv = f64::from_str(s).unwrap();
        calculator_ast::Expr::Number(
            calculator_ast::Number::from_f64(fv)
        )
    },
    <s: r"\.?[0-9]+([Ee][-+]?[0-9]+)?"> => {
        let fv = f64::from_str(s).unwrap();
        calculator_ast::Expr::Number(
            calculator_ast::Number::from_f64(fv)
        )
    },
};

match {
    r"[0-9]+",

} else {
    r"\.?[0-9]+([Ee][-+]?[0-9]+)?",

    r"[a-zA-Z][a-zA-Z0-9]*" => VarName,

    "if" => IF,
    "then" => THEN,
    "else" => ELSE,
    
    // skip whitespaces
    r"\s*" => { },
    r"//[^\n\r]*[\n\r]*" => { }, // `// comment`
    r"/\*([^\*]*\*+[^\*/])*([^\*]*\*+|[^\*])*\*/" => { }, // `/* comment */`


    _
}